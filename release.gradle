apply plugin: 'maven-publish'
apply plugin: 'signing'

archivesBaseName = project.findProperty('artifactId')

tasks.maybeCreate('sourceJar', Jar).configure {
    from java.sourceSets.main.allJava
}

publishing {

    repositories {
        maven {
            name = "Sonatype"
            if (rootProject.version.contains("SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = project.hasProperty("NEXUS_USERNAME") ? "$NEXUS_USERNAME" : System.getenv("NEXUS_USERNAME")
                password = project.hasProperty("NEXUS_PASSWORD") ? "$NEXUS_PASSWORD" : System.getenv("NEXUS_PASSWORD")
            }
        }

        maven {
            name 'test'
            url rootProject.file('build/repository').toURI().toString()
        }
    }
    
    publications {
        Publication(MavenPublication) {
            from project(':' + project.name).components.java
            groupId 'com.bugsnag'
            artifactId project.findProperty('artifactId')
            version rootProject.version
            artifact sourceJar {
                classifier "sources"
            }
            pom {
                name = project.findProperty('projectName')
                description = project.findProperty('projectDescription')
                url = 'https://github.com/bugsnag/bugsnag-java'

                scm {
                    url = 'https://github.com/bugsnag/bugsnag-java'
                    connection = 'scm:git:git://github.com/bugsnag/bugsnag-java.git'
                    developerConnection = 'scm:git:ssh://git@github.com/bugsnag/bugsnag-java.git'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }

                organization {
                    name = 'Bugsnag'
                    url = 'https://bugsnag.com'
                }

                developers {
                    developer {
                        id = 'loopj'
                        name = 'James Smith'
                        email = 'james@bugsnag.com'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications
    }
}
